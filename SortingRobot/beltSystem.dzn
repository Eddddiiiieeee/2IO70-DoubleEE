import interfaces.dzn;
import ITimer.dzn;

interface IBelt {
	in void start();
	in void stop();
	out void error();
	
	behaviour {
		enum State {Idle, Running};
		State state = State.Idle;
		
		[state.Idle] {
			on start: state = State.Running;
			on stop: illegal;
		}
		
		[state.Running] {
			on start: illegal;
			on stop: state = State.Idle;
			on optional: error;
		}
	}
}

component Belt {
	provides IBelt belt;
	
	system {
		BeltController controller;
		Motor motor;
		Sensor sensor;
		Timer timer;
		
		belt <=> controller.controller;
		controller.motor <=> motor.motor;
		controller.sensor <=> sensor.sensor;
		controller.timer <=> timer.iTimer;
	}
}

component BeltController {
	provides IBelt controller;
	
	requires IMotor motor;
	requires IBasicSensor sensor;
	requires ITimer timer;
	
	behaviour {
		enum State {Idle, Running, Error};
		State state = State.Idle;
		
		[state.Idle] {
			on controller.start(): {
				motor.start();
				timer.start($10000$);
				state = State.Running;
			}
		}
		
		[state.Running] {
			on controller.stop(): {
				motor.stop();
				timer.cancel();
				state = State.Idle;
			}
			
			on timer.timeout(): {
				state = State.Error;
				controller.error();
				motor.stop();
			}
		}
		
		[state.Error] {
			on controller.stop(): {
				//motor.stop();
				state = State.Idle;
			}
		}
	}
}

component Motor {
	provides IMotor motor;
	
	behaviour {
		enum State {Idle, Running};
		State state = State.Idle;
		
		[state.Idle] {
			on motor.start(): state = State.Running;
		}
		
		[state.Running] {
			on motor.stop(): state = State.Idle;
		}
	}
}

component Sensor {
	provides IBasicSensor sensor;
	
	// Manual implementation in C++
}